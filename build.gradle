group 'org.liamjd'
version '0.0.1-SNAPSHOT'

buildscript {
    // declare some versions
    ext.kotlin_version = '1.2.50'
    ext.spark_kotlin_version = '1.0.0-alpha'
    ext.thymeleaf_version = '3.0.9.RELEASE'
    ext.kodein_version = '4.1.0'
    ext.exposed_version = '0.10.3'
    ext.wdtk_version = '0.8.0'

    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    // for exposed database
    maven { url "http://dl.bintray.com/kotlin/exposed" }
}

dependencies {
    // Kotlin language
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: "$kotlin_version"
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: "$kotlin_version"

    // spark web framework
    compile group: 'com.sparkjava', name: 'spark-kotlin', version: "$spark_kotlin_version"
    // Thymeleaf templating engine
    compile group: 'org.thymeleaf', name:"thymeleaf", version: "$thymeleaf_version"
    compile group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-java8time', version: '3.0.0.RELEASE'
    // kodein dependency injection
    compile group: 'com.github.salomonbrys.kodein', name:"kodein", version: "$kodein_version"
    // sl4j logging
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.2'
    // database - jetbrain's exposed
    compile group: 'org.jetbrains.exposed', name: 'exposed', version: "$exposed_version"
    compile group: 'org.jetbrains.exposed', name: 'spring-transaction', version:"$exposed_version"
    compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.0.3'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.1.3'
    // reflections for annotation scanning
    compile group: 'org.reflections', name: 'reflections', version: "0.9.11"
    // json parsing
    compile 'com.beust:klaxon:0.30'
    // wikidata and sparql
    compile group: 'org.wikidata.wdtk', name: 'wdtk-wikibaseapi', version:"$wdtk_version"

    // testing
    testCompile 'junit:junit:4.12'
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar{
    // required for building JAR files and deploying to Heroku
    baseName = 'cantilevers'
    version = '0.0.1'
    manifest {
        attributes(
                'Main-Class': 'org.liamjd.cantilevers.MainKt'
        )
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task stage {
    // required for deploying on to Heroku
    dependsOn(jar)
}